# 格物第十

老子云。道生一。一生二。二生三。三生萬物。列子云。凡有貌象聲色者。皆物也。物之既有此眾端者。數言爻列術或不足表。故作一類。特名之曰物。是物者。所以合他類以狀一物之諸端者也。譬人之姓字者。言也。人之年齒者。數也。人有男女。陰陽也。或有所蓄。列也。或傍一技。術也。合而謂之人。譬石之白。色也。石之堅。質也。色質者。數也。合而謂之石。離合萬物。物物之道也。禮曰。致知在格物。物格而後知致。

今且以白馬為例。示物之叙也。

```
吾有一物。名之曰「白馬」。其物如是。
	物之「「色」」者。言曰「「白」」。
	物之「「名」」者。言曰「「白義」」。
	物之「「日行里數」」者。數曰千。
	物之「「夜行里數」」者。數曰八百。
	物之「「良耶」」者。爻曰陽。
	物之「「足數」」者。數曰四。
是謂「白馬」之物也。
```

其物既敘。復得取其端書如是。

```
夫「白馬」之「「名」」。書之。
夫「白馬」之「「日行里數」」。書之。
```

易之用之。無少異於諸變數。

```
夫「白馬」之「「日行里數」」。除其以二。昔之「白馬」之「「日行里數」」者。今其是矣。
夫「白馬」之「「夜行里數」」。減其以五百。昔之「白馬」之「「夜行里數」」者。今其是矣。
注曰。「「斯馬老矣」」。
```

或問曰。今有駿馬千匹。似此一一敘之者。不亦煩乎。曰。以術生之。或謂造物之術。

```
吾有一術。名之曰「生馬」。欲行是術。必先得二言。曰「馬名」。曰「毛色」。乃行是術曰。
  吾有一物。名之曰「馬」。其物如是。
    物之「「色」」者。言曰「毛色」。
    物之「「名」」者。言曰「馬名」。
    物之「「日行里數」」者。數曰千。
    物之「「夜行里數」」者。數曰八百。
    物之「「良耶」」者。爻曰陽。
    物之「「足數」」者。數曰四。
  是謂「馬」之物也。
  乃得「馬」。
是謂「生馬」之術也。
```

八駿者。同為穆天子良馬。唯毛色不同。故其術唯參馬名毛色二者。或欲增其參數。亦無不可也。施之乃聚八駿於一列。注。郭璞曰。八駿皆因其毛色以為名號耳。然踰輪山子綠耳之色不見載。世為圖畫者亦多。然皆不知所據。余既不能考。而學又淺。故從闕耳。

```
吾有一列。名之曰「八駿」。
施「生馬」於「「赤驥」」於「「赤」」。充「八駿」以其。
施「生馬」於「「盜驪」」於「「黑」」。充「八駿」以其。
施「生馬」於「「白義」」於「「白」」。充「八駿」以其。
施「生馬」於「「渠黃」」於「「黃」」。充「八駿」以其。
施「生馬」於「「華騮」」於「「赤」」。充「八駿」以其。
	
夫「八駿」之一。昔之其之「「名」」者。今「「騏驥」」是矣。

凡「八駿」中之「馬」。
	夫「馬」之「「名」」。書之也。
```

或問曰。既有一物。欲知其諸端之名。奈何。或問曰。今欲取物之一端。然其名未遽知。唯於運算中得之。奈何。或問曰。偶得一物。不知其為物耶。其為數為言為爻為術為列耶。奈何。曰引「格物」。

```
吾嘗觀「「格物」」之書。方悟「取物」「置物」「列物之端」「識類」之義。

吾有一言。曰「「日」」。注曰「「或曰「夜」」」。
名之曰「時」。
夫「白馬」。加「時」以「「行里數」」。取二以施「取物」。書之。

吾有一數。曰三萬。名之曰「新數」。注曰。「「義山詩曰。八俊日行三萬里」」
夫「白馬」。加「時」以「「行里數」」。夫「新數」。取三以施「置物」。書之。

施「列物之端」於「白馬」。注曰「「得言之列也」」
書之。

施「識類」於三。書之。注曰「「得數」」
施「識類」於陰。書之。注曰「「得爻」」
施「識類」於「白馬」。書之。注曰「「得物」」
施「識類」於「「善哉斯言」」。書之。注曰「「得言」」
```

又物者。雖貌盡同而其實或非一也。如玉刻雙璋。錦挑對褓。故以「等於」法比之。唯實一物二名方得陽。非是。皆得陰。欲以諸端同而同之者。必一一校而後可。非唯物然。列亦如是也。

```
吾有一物。名之曰「戊」。其物如是。
	物之「「甲」」者。數曰一。
	物之「「乙」」者。數曰二。
是謂「戊」之物也。

吾有一物。名之曰「戌」。其物如是。
	物之「「甲」」者。數曰一。
	物之「「乙」」者。數曰二。
是謂「戌」之物也。

若「戊」不等於「戌」者。
	吾有一言。曰「「然也」」。書之。
也。
若「戊」之「「甲」」等於「戌」之「「甲」」者。
	若「戊」之「「乙」」等於「戌」之「「乙」」者。
		吾有一言。曰「「然也」」。書之。
	也。
也。

夫「戊」。名之曰「戍」。

若「戊」等於「戍」者。
	吾有一言。曰「「然也」」。書之。
也。
```

又以物包羅諸象。或令機器寫之以存運算之所得。或使讀之以為運算之所參。罔不嘉焉。然以其辭稍煩。以所函略廣。故另為渾沌之辭。精煉其字。簡易其法。以資讀寫之便也。復作渾沌之術。得其言而化之為物。得其物而以其言載之。

渾沌者。食也。揚雄曰。餅謂之飩。今薄麵夾餡入湯者也。取其包物之義。故名。

```
吾嘗觀「渾沌經」之書。方悟「包渾沌」之義。

施「包渾沌」於「白馬」。書之。
```

得如左。渾沌凡言數者。唯列其位。譬不謂三千八百七十。但謂三八七〇耳。

```
物
　之「色」言「白」
　之「名」言「白義」
　之「日行里數」數「一〇〇〇」
　之「夜行里數」數「八〇〇」
　之「良耶」爻「陽」
　之「足數」數「四」
也
```

或列諸物而具包。輒加「諸」字於其術。今取八駿之二。書如是。

```
吾嘗觀「渾沌經」之書。方悟「包諸渾沌」之義。

吾有一列。名之曰「二駿」。
夫「八駿」之一。充「二駿」以其。
夫「八駿」之二。充「二駿」以其。

施「包諸渾沌」於「二駿」。書之。
```

得

```
列
　物
　　之「色」言「赤」
　　之「名」言「赤驥」
　　之「日行里數」數「一〇〇〇」
　　之「夜行里數」數「八〇〇」
　　之「良耶」爻「陽」
　　之「足數」數「四」
　也
　物
　　之「色」言「黑」
　　之「名」言「盜驪」
　　之「日行里數」數「一〇〇〇」
　　之「夜行里數」數「八〇〇」
　　之「良耶」爻「陽」
　　之「足數」數「四」
　也
也
```

或問曰。既得其渾沌語。欲逆得其物。曰。勞「食渾沌」「食諸渾沌」之術可矣。

```
吾嘗觀「渾沌經」之書。方悟「包渾沌」「食渾沌」之義。

施「包渾沌」於「白馬」。名之曰「渾沌」。
施「食渾沌」於「渾沌」。名之曰「非馬」。注曰。「「公孫龍子曰。白馬非馬」」

夫「非馬」之「「名」」。書之。
夫「非馬」之「「色」」。書之。
注曰。「「所得皆如白馬」」
```

今復以渾沌語書李翰林之生平如是。以示其物包物。物包列。列包物也。

```
物
  之「字」言「太白」
  之「號」言「青蓮」
  之「壽考」數「六一」
  之「生年」言「長安元年」
  之「卒年」言「上元三年」
  之「存詩」數「九〇〇」
  之「擅詩」爻「陽」
  之「官」物
    之「銜」言「翰林」。
    之「年」言「天寶元年」。
  也
  之「名句」列
    物
      之「題」言「清平調」
      之「文」言「雲想衣裳花想容。春風拂檻露華濃。」
    也
    物
      之「題」言「長相思」
      之「文」言「長相思。在長安。絡緯秋啼金井闌。微霜淒淒簟色寒。」
    也
    物
      之「題」言「襄陽歌」
      之「文」言「落日欲沒峴山西。倒著接蘺花下迷。襄陽小兒齊拍手。攔街爭唱「白銅鞮」。」
    也
    物
      之「題」言「楊叛兒」
      之「文」言「烏啼隱楊花。君醉留妾家。博山爐中沉香火。雙煙一氣凌紫霞。」
    也
  也
  之「子女」列
    言「伯禽」言「平陽」言「頗黎」
  也
也
```

格物之道。至是亦略備矣。其雖誠非編程之所必須。然條理以明。讀寫以便。孰言無益耳。